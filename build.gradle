plugins {
    id 'org.springframework.boot' version '2.7.11'
}

repositories {
    maven {
        name = "development"
        url = "${artifactoryContextUrl}/develop/"
    }
    maven {
        name = "snapshots"
        url = "${artifactoryContextUrl}/snapshots/"
    }
    maven {
        name = "releases"
        url = "${artifactoryContextUrl}/releases/"
    }
    maven {
        url = "${artifactoryContextUrl}/maven"
    }
}

apply plugin: 'checkstyle'
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.sophos.central.chassis'
apply plugin: 'com.github.spotbugs'
apply plugin: 'jacoco'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext {
  SERVICE_URL = "http://localhost"
}
// lean JAR is being used for veracode verification
jar {
    classifier= 'lean'
    enabled = true
}

apply from: 'config/sourceSets.gradle'
apply from: 'config/testTasks.gradle'
apply from: 'config/publish.gradle'
apply from: 'config/dependency-substitution.gradle'

// Veracode Scan
apply from: 'config/syncGradleTaskFiles.gradle'

boolean isJenkinsBuild = System.getenv('JOB_NAME') != null ?: false
if (!isJenkinsBuild) {
  apply from: 'config/localMonitoring.gradle'
}

dependencyUpdates {
  checkForGradleUpdate = true
  resolutionStrategy {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
          selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
        }
        if (rejected) {
          selection.reject('Release candidate')
        }
      }
    }
  }
}
